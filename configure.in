dnl Copyright (C) 2002 Ben Goodwin
dnl This file is part of the nss-mysql library.
dnl
dnl The nss-mysql library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU General Public License as published
dnl by the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl The nss-mysql library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with the nss-mysql library; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl $Id$ */

dnl Autoconf 2.53 is REQUIRED

AC_REVISION($Revision$)dnl
AC_INIT
AC_PREREQ(2.53)
AC_CONFIG_SRCDIR([nss_main.c])
AC_CANONICAL_TARGET([])
AC_PREFIX_DEFAULT()
AM_INIT_AUTOMAKE(libnss_mysql, 0.7dev)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_CC

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_ARG_WITH(mysql-inc,
            [  --with-mysql-inc=DIR    Location of your MySQL include files])
AC_ARG_WITH(mysql-lib,
            [  --with-mysql-lib=DIR    Location of your MySQL libraries])
AC_ARG_WITH(log-facility,
            [  --with-log-facility=ARG Log to specified facility])
AC_ARG_ENABLE(debug,
            [  --enable-debug          Enable debug (see DEBUGGING)],
            [AC_DEFINE([DEBUG], 1, [Enable debug])])

test -z "$with_log_facility" && with_log_facility=LOG_AUTH
AC_DEFINE_UNQUOTED([SYSLOG_FACILITY], $with_log_facility,
                   [Log important messages to this facility])

case "$target_os" in
linux*)
    LIBVER=2
    TOS=linux
    test "$prefix" = "NONE" && prefix=
    ;;
solaris*)
    LIBVER=1
    TOS=solaris
    test "$prefix" = "NONE" && prefix= && libdir=/usr/lib
    ;;
*) os=unknown ;;
esac

AC_CACHE_CHECK([whether the linker accepts -Bgroup],
               [nss_mysql_cv_cc_bgroup], [
    SAVELIBS=$LIBS
    LIBS="-Wl,-Bgroup $SAVELIBS"
    AC_TRY_LINK([], [], nss_mysql_cv_cc_bgroup=yes,
                        nss_mysql_cv_cc_bgroup=no)
    LIBS=$SAVELIBS])
if test $nss_mysql_cv_cc_bgroup = "yes"; then
    LIBS="-Wl,-Bgroup $SAVELIBS"
fi

AC_CACHE_CHECK([whether the linker accepts --allow-shlib-undefined],
               [nss_mysql_cv_cc_shlibundef], [
    SAVELIBS=$LIBS
    LIBS="-Wl,--allow-shlib-undefined $SAVELIBS"
    AC_TRY_LINK([], [], nss_mysql_cv_cc_shlibundef=yes,
                        nss_mysql_cv_cc_shlibundef=no)
    LIBS=$SAVELIBS])
if test $nss_mysql_cv_cc_shlibundef = "yes"; then
    LIBS="-Wl,--allow-shlib-undefined $SAVELIBS"
fi

AC_CACHE_CHECK([whether the linker accepts -znodelete],
               [nss_mysql_cv_cc_znodelete], [
    SAVELIBS=$LIBS
    LIBS="-Wl,-znodelete $SAVELIBS"
    AC_TRY_LINK([], [], nss_mysql_cv_cc_znodelete=yes,
                        nss_mysql_cv_cc_znodelete=no)
    LIBS=$SAVELIBS])
if test $nss_mysql_cv_cc_znodelete = "yes"; then
    LIBS="-Wl,-znodelete $SAVELIBS"
fi

AM_CONDITIONAL(LINUX, test "$TOS" = "linux")
AM_CONDITIONAL(SOLARIS, test "$TOS" = "solaris")

AC_SUBST(LIBVER)

FIND_MYSQL()

AC_CHECK_LIB(socket, getsockname)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(m, floor)
AC_CHECK_LIB(dl, dlsym)
AC_CHECK_LIB(z, compress)

AC_CHECK_LIB(mysqlclient_r, main, ,
             [AC_CHECK_LIB(mysqlclient, main, ,
                           [AC_MSG_ERROR([Unable find a functioning MySQL library])])])

AC_HEADER_STDC
AC_CHECK_HEADER(mysql.h, , [AC_MSG_ERROR([Unable to find mysql.h])])
AC_CHECK_HEADERS(syslog.h stdint.h nss.h nss_common.h strings.h)

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T
AC_CHECK_TYPES([socklen_t], , , [
#include <unistd.h>
#include <sys/socket.h>
])

AC_CHECK_FUNCS(strcspn strspn, ,
               [AC_MSG_ERROR([The above function is required])])
AC_CHECK_FUNCS(mysql_init)

EXPANDED_SYSCONFDIR=`eval echo $sysconfdir`
AC_DEFINE_UNQUOTED([MAINCFG], "$EXPANDED_SYSCONFDIR/nss_mysql.cfg",
                   [Main config file])
AC_DEFINE_UNQUOTED([ROOTCFG],"$EXPANDED_SYSCONFDIR/nss_mysql_root.cfg",
                   [Root config file])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
