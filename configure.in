dnl Copyright (C) 2002 Ben Goodwin
dnl This file is part of the nss-mysql library.
dnl
dnl The nss-mysql library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU General Public License as published
dnl by the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl The nss-mysql library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with the nss-mysql library; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl $Id$ */

AC_REVISION($Revision$)dnl
AC_INIT(nss_main.c)
AM_INIT_AUTOMAKE(libnss_mysql, 0.3dev)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_LIBTOOL

AC_ARG_WITH(mysql-inc, [  --with-mysql-inc=DIR    Location of your MySQL include files],
[
case "${withval}" in
 yes) ;;
  no) ;;
   *) MYSQL_INCLUDE_DIR=${withval} ;;
esac
])
AC_ARG_WITH(mysql-lib, [  --with-mysql-lib=DIR    Location of your MySQL libraries],
[
case "${withval}" in
 yes) ;;
  no) ;;
   *) MYSQL_LIB_DIR=${withval} ;;
esac
])
if test -n "$MYSQL_INCLUDE_DIR"; then
  CPPFLAGS="-I $MYSQL_INCLUDE_DIR $CPPFLAGS"
  export CPPFLAGS
fi
if test -n "$MYSQL_LIB_DIR"; then
  LIBS="-L$MYSQL_LIB_DIR $LIBS"
  export LIBS
fi
AC_CHECK_LIB(mysqlclient, mysql_connect, ,
             [AC_MSG_ERROR(Unable find a functioning MySQL library)])

AC_HEADER_STDC
AC_CHECK_HEADERS(syslog.h unistd.h mysql/mysql.h mysql.h nss.h strings.h)

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T
AC_TRY_COMPILE([
#include <unistd.h>],
[intptr_t foo;],
[have_intptr_t=yes],
[have_intptr_t=no])
case "$have_intptr_t" in
  yes) ;;
   no) AC_DEFINE_UNQUOTED(intptr_t, int) ;;
esac

AC_CHECK_FUNCS(strcspn strspn strtok, ,
               [AC_MSG_ERROR(The above function is required)])
AC_CHECK_FUNCS(mysql_connect mysql_init)

AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)

AC_TRY_COMPILE([
#include <stdio.h>],
[printf ("Foo: %s\n", __func__);],
[have_func=yes],
[have_func=no])
case "$have_func" in
  yes) AC_DEFINE(HAVE___FUNC__) ;;
   no) ;;
esac

AC_TRY_COMPILE([
#include <syslog.h>],
[int foo = LOG_AUTHPRIV;],
[have_authpriv=yes],
[have_authpriv=no])
case "$have_authpriv" in
  yes) AC_DEFINE(HAVE_LOG_AUTHPRIV) ;;
   no) ;;
esac

test "$prefix" = "NONE" && prefix=/usr/local
EXPANDED_SYSCONFDIR=`eval echo $sysconfdir`
AC_DEFINE_UNQUOTED(MAINCFG,"$EXPANDED_SYSCONFDIR/nss_mysql.cfg")
AC_DEFINE_UNQUOTED(ROOTCFG,"$EXPANDED_SYSCONFDIR/nss_mysql_root.cfg")

AC_OUTPUT(Makefile)
