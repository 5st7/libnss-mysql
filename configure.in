dnl Copyright (C) 2002 Ben Goodwin
dnl This file is part of the nss-mysql library.
dnl
dnl The nss-mysql library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU General Public License as published
dnl by the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl The nss-mysql library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with the nss-mysql library; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl $Id$ */

AC_REVISION($Revision$)dnl
AC_INIT(nss_main.c)
AC_CANONICAL_SYSTEM
AC_PREFIX_DEFAULT()
AM_INIT_AUTOMAKE(libnss_mysql, 0.3)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_CC

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_ARG_WITH(mysql-inc, [  --with-mysql-inc=DIR    Location of your MySQL include files],
[
case "${withval}" in
 yes) ;;
  no) ;;
   *) MYSQL_INCLUDE_DIR=${withval} ;;
esac
])
AC_ARG_WITH(mysql-lib, [  --with-mysql-lib=DIR    Location of your MySQL libraries],
[
case "${withval}" in
 yes) ;;
  no) ;;
   *) MYSQL_LIB_DIR=${withval} ;;
esac
])

case "$target_os" in
linux*)
    dnl Shell cmd below adapted from padl.com's nss_ldap
    LIBVER=`ls /lib/libnss_files.so.? | tail -1 | sed -e 's#/lib/libnss_files\.so\.\(.*\)#\1#'`
    TOS=linux
    test "$prefix" = "NONE" && prefix=
    ;;
solaris*)
    LIBVER=1
    TOS=solaris
    if test "$ac_cv_prog_gcc" = "yes"; then
        CPPFLAGS="-m32 $CPPFLAGS"
    fi
    test "$prefix" = "NONE" && prefix= && libdir=/usr/lib
    ;;
*) os=unknown ;;
esac

AM_CONDITIONAL(LINUX, test "$TOS" = "linux")
AM_CONDITIONAL(SOLARIS, test "$TOS" = "solaris")

AC_SUBST(LIBVER)

FIND_MYSQL()

AC_CHECK_LIB(mysqlclient, mysql_connect, ,
             [AC_MSG_ERROR(Unable find a functioning MySQL library)])

AC_CHECK_HEADER(mysql.h, , [AC_MSG_ERROR(Unable to find mysql.h)])

AC_HEADER_STDC
AC_CHECK_HEADERS(syslog.h unistd.h nss.h nss_common.h strings.h)

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T
AC_TRY_COMPILE([
#include <unistd.h>],
[uintptr_t foo;],
[have_uintptr_t=yes],
[have_uintptr_t=no])
case "$have_uintptr_t" in
  yes) ;;
   no) AC_DEFINE_UNQUOTED(uintptr_t, unsigned int) ;;
esac

AC_CHECK_FUNCS(strcspn strspn strtok, ,
               [AC_MSG_ERROR(The above function is required)])
AC_CHECK_FUNCS(mysql_connect mysql_init)

AC_TRY_COMPILE([
#include <stdio.h>],
[printf ("Foo: %s\n", __func__);],
[have_func=yes],
[have_func=no])
case "$have_func" in
  yes) AC_DEFINE(HAVE___FUNC__) ;;
   no) ;;
esac

AC_TRY_COMPILE([
#include <syslog.h>],
[int foo = LOG_AUTHPRIV;],
[have_authpriv=yes],
[have_authpriv=no])
case "$have_authpriv" in
  yes) AC_DEFINE(HAVE_LOG_AUTHPRIV) ;;
   no) ;;
esac

AC_TRY_COMPILE([
#include <syslog.h>],
[int foo = LOG_FTP;],
[have_logftp=yes],
[have_logftp=no])
case "$have_logftp" in
  yes) AC_DEFINE(HAVE_LOG_FTP) ;;
   no) ;;
esac

EXPANDED_SYSCONFDIR=`eval echo $sysconfdir`
AC_DEFINE_UNQUOTED(MAINCFG,"$EXPANDED_SYSCONFDIR/nss_mysql.cfg")
AC_DEFINE_UNQUOTED(ROOTCFG,"$EXPANDED_SYSCONFDIR/nss_mysql_root.cfg")

AC_OUTPUT(Makefile)
